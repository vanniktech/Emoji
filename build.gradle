plugins {
  alias(libs.plugins.android.library) apply false
  alias(libs.plugins.android.application) apply false
  alias(libs.plugins.codequality) apply true
  alias(libs.plugins.kotlin.android) apply false
  alias(libs.plugins.kotlin.parcelize) apply false
  alias(libs.plugins.publish) apply false
}

codeQualityTools {
  lint {
    lintConfig = rootProject.file("lint.xml")
    checkAllWarnings = true
    textReport = true
  }
  checkstyle {
    enabled = false // Kotlin only.
  }
  pmd {
   enabled = false // Kotlin only.
  }
  ktlint {
    toolVersion = libs.versions.ktlint.get()
  }
  detekt {
    enabled = false
  }
  cpd {
    enabled = false
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
  }
}

wrapper {
  gradleVersion = libs.versions.gradle.get()
  distributionType = Wrapper.DistributionType.ALL
}

allprojects {
  plugins.withId("com.vanniktech.maven.publish.base") {
    group = "com.vanniktech"
    version = VERSION_NAME

    mavenPublishing {
      publishToMavenCentral("DEFAULT")
      signAllPublications()
      pomFromGradleProperties()
    }
  }
}
