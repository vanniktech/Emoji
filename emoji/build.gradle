plugins {
  id("org.jetbrains.dokka")
  id("org.jetbrains.kotlin.multiplatform")
  id("com.android.library")
  id("org.jetbrains.kotlin.plugin.parcelize")
  id("me.tylerbwong.gradle.metalava")
  id("com.vanniktech.maven.publish")
  id("app.cash.licensee")
}

licensee {
  allow("Apache-2.0")
}

metalava {
  filename.set("api/current.txt")
  sourcePaths.setFrom("src/commonMain", "src/androidMain")
}

// CocoaPods requires a version.
version = VERSION_NAME

kotlin {
  androidTarget("android") {
    publishLibraryVariants("release")
  }
//  ios("ios")
  jvm("jvm")
  jvmToolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }

  sourceSets {
    commonMain {
      dependencies {
      }
    }

    commonTest {
      dependencies {
        implementation libs.kotlin.test.common
        implementation libs.kotlin.test.annotations.common
      }
    }

    androidMain {
      dependencies {
        api libs.androidx.appcompat
        api libs.androidx.cardview
        api libs.androidx.recyclerview
        api libs.ui
      }
    }

    androidUnitTest {
      dependencies {
        implementation libs.kotlin.test.junit
        implementation libs.robolectric
      }
    }

    jvmMain.dependencies {
    }

    jvmTest.dependencies {
      implementation libs.kotlin.test.junit
    }
  }
}

android {
  namespace "com.vanniktech.emoji"

  compileSdk(libs.versions.compileSdk.get() as int)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  defaultConfig {
    minSdkVersion libs.versions.minSdk.get() as int
  }

  resourcePrefix 'emoji_'
}
