plugins {
  id("org.jetbrains.dokka")
  id("org.jetbrains.kotlin.multiplatform")
  id("com.android.library")
  id("org.jetbrains.kotlin.plugin.parcelize")
  id("me.tylerbwong.gradle.metalava")
  id("com.vanniktech.maven.publish")
  id("app.cash.licensee")
}

licensee {
  allow("Apache-2.0")
}

metalava {
  filename.set("api/current.txt")
  sourcePaths.setFrom("src/commonMain", "src/androidMain", "src/jvmMain")
}

// CocoaPods requires a version.
version = VERSION_NAME

kotlin {
  android("android") {
    publishLibraryVariants("release")
  }
//  ios("ios")
  jvm("jvm")
  jvmToolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }

  sourceSets {
    commonMain {
      dependencies {
        api project(":emoji")
      }
    }

    commonTest {
      dependencies {
        implementation libs.kotlin.test.common
        implementation libs.kotlin.test.annotations.common
      }
    }

    androidMain.dependencies {
    }

    androidUnitTest.dependencies {
      implementation libs.kotlin.test.junit
      implementation libs.robolectric
    }

    jvmMain.dependencies {
    }

    jvmTest.dependencies {
      implementation libs.kotlin.test.junit
    }
  }
}

android {
  namespace "com.vanniktech.emoji.facebook"

  compileSdkVersion libs.versions.compileSdk.get() as int

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  defaultConfig {
    minSdkVersion libs.versions.minSdk.get() as int
  }

  resourcePrefix 'emoji_facebook_'
}
